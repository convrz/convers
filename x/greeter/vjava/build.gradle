plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.2'
	id 'io.spring.dependency-management' version '1.1.7'
	id "com.google.protobuf" version "0.9.4"
}

group = 'com.github.convrz.convers'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(23)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	implementation 'com.google.protobuf:protobuf-java:4.28.2'
	implementation 'io.grpc:grpc-stub:1.15.1'
	implementation 'io.grpc:grpc-protobuf:1.15.1'
	if (JavaVersion.current().isJava9Compatible()) {
		// Workaround for @javax.annotation.Generated
		// see: https://github.com/grpc/grpc-java/issues/3633
		implementation 'javax.annotation:javax.annotation-api:1.3.1'
	}

}

protobuf {
	protoc {
		// The artifact spec for the Protobuf Compiler
		artifact = 'com.google.protobuf:protoc:4.28.2'
	}
	plugins {
		// Optional: an artifact spec for a protoc plugin, with "grpc" as
		// the identifier, which can be referred to in the "plugins"
		// container of the "generateProtoTasks" closure.
		grpc {
			artifact = 'io.grpc:protoc-gen-grpc-java:1.15.1'
		}
	}
	generateProtoTasks {
		ofSourceSet('main').configureEach {
			plugins {
				// Apply the "grpc" plugin whose spec is defined above, without
				// options.  Note the braces cannot be omitted, otherwise the
				// plugin will not be added. This is because of the implicit way
				// NamedDomainObjectContainer binds the methods.
				grpc { }
			}
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
}